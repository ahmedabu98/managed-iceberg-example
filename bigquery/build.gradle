plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.example.bigquery'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.confluent.io/maven/")
    }
}

dependencies {
    // Apache Beam's IcebergIO
    implementation "org.apache.beam:beam-sdks-java-core:2.60.0"
    implementation "org.apache.beam:beam-sdks-java-managed:2.60.0"
    implementation "org.apache.beam:beam-sdks-java-io-iceberg:2.60.0"
    implementation project(":utils")

    // runtime dependencies when writing to GCS
    runtimeOnly "com.google.cloud.bigdataoss:gcs-connector:hadoop2-2.2.16"
    runtimeOnly "org.apache.hadoop:hadoop-client:2.10.2"

    // Shaded BigQueryMetastore catalog jar with some problematic packages relocated
    implementation project(path:":bigquery:bqms", configuration:"shadow")

    // to run the pipeline with direct runner
    runtimeOnly 'org.apache.beam:beam-runners-direct-java:2.60.0'
    implementation 'org.apache.beam:beam-runners-google-cloud-dataflow-java:2.60.0'
}

tasks.create(name:"execute", type:JavaExec) {
    main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "NONE"
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
    args project.hasProperty("exec.args") ? project.getProperty("exec.args").split() : []
}
