plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

group = 'org.example.hive'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.confluent.io/maven/")
    }
}

def iceberg_version = "1.4.2"
def hive_version = "3.1.3"

dependencies {
    // Apache Beam's IcebergIO
    implementation "org.apache.beam:beam-sdks-java-core:2.59.0"
    implementation "org.apache.beam:beam-sdks-java-io-iceberg:2.59.0"
    implementation project(":utils")

    // runtime dependencies when writing to GCS
    runtimeOnly "com.google.cloud.bigdataoss:gcs-connector:hadoop2-2.2.16"
    runtimeOnly "org.apache.hadoop:hadoop-client:2.10.2"

    // hive runtime dependencies
    runtimeOnly project(path: ":hive:exec", configuration: "shadow")
    runtimeOnly "org.apache.iceberg:iceberg-hive-metastore:$iceberg_version"

    // metastore utils (in case hive metastore needs to be created)
    implementation (project(":hive:metastoreutils"))

    // to run the pipeline with DirectRunner or DataflowRunner
    runtimeOnly 'org.apache.beam:beam-runners-direct-java:2.59.0'
    implementation 'org.apache.beam:beam-runners-google-cloud-dataflow-java:2.59.0'
}

tasks.create(name:"execute", type:JavaExec) {
    main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "NONE"
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
    args project.hasProperty("exec.args") ? project.getProperty("exec.args").split() : []
}
